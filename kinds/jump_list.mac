/*ファイルの行と列番号へジャンプするカインド
*/
#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg1=getarg(1);
$arg2=getarg(2);
$arg3=getarg(3);
$arg4=getarg(4);
$arg5=getarg(5);


call $label, $arg1, $arg2, $arg3, $arg4, $arg5 ;
endmacro $$return;


do_action:
	$$action=$$1;
	call $$action;
	return $$return;


open:
	##current_hidemaru_handle=hidemaruhandle(0);
	##new_hidemaru_handle=0;

    $$macro_root_dir=dllfuncstrw(#g_dll_ohtorii_tools,"StaticStatusGetRootMacroDirectory");
    $$macro_path=$$macro_root_dir+"\\internal\\show_message.mac";

	##count=dllfuncw(#g_dll_ohtorii_tools,"GetSelectionCount");
	##i=0;
	while(##i < ##count){
		$$filename=dllfuncstrw(#g_dll_ohtorii_tools,"GetSelectionActionFileName",##i);
		call check_file_exists $$filename;
		if(##return){
			##column=dllfuncw(#g_dll_ohtorii_tools,"GetSelectionActionColumn",##i);
			##line  =dllfuncw(#g_dll_ohtorii_tools,"GetSelectionActionLine",##i);

			openfile $$filename;
			if(getresultex(25)==false){
                //openfileに成功
			    moveto2 ##column,##line;
			    ##new_hidemaru_handle=hidemaruhandle(0);
			}

			setactivehidemaru ##current_hidemaru_handle;
		}else{
			$$message="ファイルを見付けることが出来ませんでした。\nもしかしたら既に削除されているのかもしれません。\n\n（ファイル名）\n" + $$text;
			execmacro $$macro_path, "error", $$message;
		}

		##i = ##i + 1;
	}

	if(##new_hidemaru_handle){
		##ret = dllfuncw(#g_dll_ohtorii_tools,"AppendCallBack_ProcessFinish",currentmacrofilename,"callback_open",str(##new_hidemaru_handle));
	}
	return str(##ret);


check_file_exists:
	$$filename=$$1;
	if(existfile($$filename)){
		if((existfile($$filename,1)&0x00000010) == 0){
			//パスが存在している、かつ、ファイルである
			return true;
		}
	}
	return false;


callback_open:
	##handle=val($$1);
	setactivehidemaru ##handle;
	return str(##return);


/*ファイル

引数	ルートフォルダ名
		空白の場合はカレントディレクトリを使用する。
*/

$g_user_data_name = "file.current_dir";

#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg1=getarg(1);
$arg2=getarg(2);
$arg3=getarg(3);
$arg4=getarg(4);
$arg5=getarg(5);

$g_root_macro_directory =dllfuncstrw(#g_dll_ohtorii_tools,"StaticStatusGetRootMacroDirectory");


call $label, $arg1, $arg2, $arg3, $arg4, $arg5;
endmacro $$return;


/*
\ 'ignore_globs' : [
      \         '.', '..', '*~', '*.o', '*.exe', '*.bak',
      \         'DS_Store', '*.pyc', '*.sw[po]', '*.class',
      \         '.hg/**', '.git/**', '.bzr/**', '.svn/**', '.vs/**',
*/
gather_candidates:
	$$root_dir=$$1;
	call gather_candidates_main,$$root_dir;
	$$ret=$$return;
	return $$ret;


gather_candidates_main:
	$$sub_dir=$$1;

	/*memo: カレントディレクトリをユーザーデータに保持しています。
	*/
	if($$sub_dir==""){
		$$current_dir=directory+"\\";
		##r=dllfuncw(#g_dll_ohtorii_tools,"SetUserDataString",$g_user_data_name,$$current_dir);
	}

	$$parent_dir= dllfuncstrw(#g_dll_ohtorii_tools,"GetUserDataString",$g_user_data_name,"");
	$$root_dir 	= $$parent_dir+$$sub_dir;
	##r=dllfuncw(#g_dll_ohtorii_tools,"SetUserDataString",$g_user_data_name,$$root_dir);
	//memo: ディレクトリ区切り(\)で終端させる。
	$$directory_separator=wcsrightstr($$root_dir,1);
	if(($$directory_separator!="\\") && ($$directory_separator!="/")){
		$$root_dir = $$root_dir + "\\";
	}

	//////////////////////////////////////////////////////////////////////////
	//ディレクトリを登録する
	//////////////////////////////////////////////////////////////////////////
	##r	= dllfuncw(#g_dll_ohtorii_tools, "BeginSource", "directory");
	if(!##r){
		return "0";
	}
	//カレントディレクトリを分かりやすくするために、候補ヘッダにカレントディレクトリを設定する。
	call GetAbsolutePathName $$root_dir;
	$$abs_root_dir=$$return;
	##r = dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateHeader", "["+$$abs_root_dir+"]", "");
	if(!##r){
		return "0";
	}
	call AppendDirectories $$root_dir;
    ##r	= dllfuncw(#g_dll_ohtorii_tools, "EndSource");
	if(!##r){
		return "0";
	}

	//////////////////////////////////////////////////////////////////////////
	//ファイルを登録する
	//////////////////////////////////////////////////////////////////////////
	##r	= dllfuncw(#g_dll_ohtorii_tools, "BeginSource", "file");
	if(!##r){
		return "0";
	}
	call AppendFolders $$root_dir;
	##r	= dllfuncw(#g_dll_ohtorii_tools, "EndSource");
	if(!##r){
		return "0";
	}

	return "1";


AppendDirectories:
	$$root_dir = $$1;

	//(memo) ..はVBSで列挙されないため手動で追加する
	$$dir="..\\";
	##r=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$dir, "");
	##r=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionDirectoryName", $$root_dir+$$dir);

	//通常の処理
	##objFSO		= createobject("Scripting.FileSystemObject");
	##objFolder 	= callmethod_returnobj(##objFSO,"GetFolder",$$root_dir);
	##objSubFolders = getpropobj(##objFolder,"SubFolders");
	while(1){
		##objFile = getcollection(##objSubFolders);
		if(##objFile==0){
			break;
		}
		$$dir=getpropstr(##objFile,"Name")+"\\";
		releaseobject ##objFile;

		##r=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$dir, "");
		##r=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionDirectoryName", $$root_dir+$$dir);
	}
	return ;


AppendFolders:
	$$root_dir=$$1;
	##objFSO	= createobject("Scripting.FileSystemObject");
	##objFolder = callmethod_returnobj(##objFSO,"GetFolder",$$root_dir);
	##objFiles	= getpropobj(##objFolder,"Files");
	while(1){
		##objFile = getcollection(##objFiles);
		if(##objFile==0){
			break;
		}
		$$file=getpropstr(##objFile,"Name");
		releaseobject ##objFile;

		##r=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$file, "");
		##r=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionFileName", $$root_dir+$$file);
	}
	releaseobject ##objFiles;
	releaseobject ##objFolder;
	releaseobject ##objFSO;

	return ;


GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
	return $$abs_filename;

/* Unityマクロ　メイン処理

usage:
	main.mac root_dir sources options sources_args

引数:
	root_dir	unityマクロのルートディレクトリ

	sources 	タブ区切りのソース名
				（書式）ソース名1\tソース名2...
				（例）  file_mru\tprocess\ttab

    options     動作オプション
                （書式）オプション1\nオプション2\n...
                （例）  @"auto-preview=on
	                     default-action=insert_file"

    sources_args ソース毎の引数
                （書式）ソース1の引数1\tソース1の引数2...\nソース2の引数1\t...

                （例  その1）
                    sources        : file_mru
                    file_mruの引数 : arg1 arg2
                    sources_args   : arg1\targ2

                （例  その2）
                    sources        : tab window
                    tabの引数      : arg1 arg2
                    windowの引数   : arg3
                    sources_args   : arg1\targ2\narg3

*/
if(version<891){
	message
	"秀丸エディタのバージョンが古いためマクロを実行できません。\n"	+
	"本マクロのドキュメントに記載されたバージョン以降の秀丸エディタをお使いください。";
	endmacro "0";
}

#g_tick_prev=tickcount;

/*****************************************************************************
	グローバル変数
*****************************************************************************/
#g_open_newfile = false;
#g_new_hidemaru = 0;
#g_target_hidemaru = hidemaruhandle(0);

/*候補を再取得するソース名

(Ex.) 	$g_reget_sourcenames[0]="file_mru";
		$g_reget_sourcenames[1]="process";
		#g_reget_sourcenames_count=2;
*/
$g_reget_sourcenames[0]="";
#g_reget_sourcenames_count=0;

//DLL
#g_hmjre_dll		=0;
#g_dll_ohtorii_tools=0;
#g_dll_hm_process	=0;
#g_dll_output_panel =0;


/*****************************************************************************
	その他グローバル変数
*****************************************************************************/
//マクロのルートディレクトリ
$g_root_macro_directory=getarg(0);


/*****************************************************************************
	グローバル変数(const)
*****************************************************************************/
//command-lineへのパス
$g_command_line_path=currentmacrodirectory+"\\3rd_party\\command-line\\command-line-internal";
//空白文字の定義
$g_space = "\r\n\t 　";

//返値
#g_result_cancel		=0;
#g_result_success		=1;
#g_result_continue		=2;
#g_result_quit			=3;
#g_result_faile 		=-1;

//現在の候補モード
#g_candidate_mode_text		=0;
#g_candidate_mode_action	=1;



//マーク
$g_color_mark="gather";

//動作オプション
#g_option_auto_preview=false;
$g_option_default_action="";
$g_option_auto_preview_context_candidate="";

/*キー入力の間隔(単位＝ミリ秒)
(memo) 数値はiniファイルで設定する。
*/
#keyinput_interval = 0;

/*****************************************************************************
	メイン処理
*****************************************************************************/
//
//動作環境を固定化する。
//
setcompatiblemode
	0x00000002 |	/* 折りたたみを無視してなるべく維持 */
	0x00000008 |	/* 部分編集を無視してなるべく維持 */
	0x00000030 |	/* 同じファイルを２つ開くことを禁止する（ジャンプする） */
	0x00000200 |	/*検索での表示　→　範囲選択*/
	0x00002000 |	/*貼り付け後のカーソル位置　→　最後に移動 */
	0x00020000 ;	/*マクロ終了後に検索条件をマクロ実行前と同じに戻します。（V8.20以降） */

if(893<=version){
	##old = compatiblemode;
	setcompatiblemode ##old | 0x04000000;	//　execmacroのコンパイル内容をメモリ上にキャッシュして再利用します。（V8.92β12以降）
}

#g_target_hidemaru  = hidemaruhandle(0);
//disableerrormsg;
debuginfo 0;
disablebreak;
disablehistory
				//0x0001 /*ファイルヒストリ*/
				//0x0002 /*フォルダヒストリ*/
				0x0004 | /*検索ヒストリ*/
				0x0008 | /*置換ヒストリ*/
				0x0010 | /*grepファイルヒストリ*/
				0x0020 | /*プログラム実行ヒストリ*/
				0x0040 ; /*クリップボード履歴*/
				//0x0080 | /*/nオプション,またはopenfileのnoaddhistと同じ扱い*/
				//0x0100 ; /*今開いているファイル(自分自身)をヒストリに残さないようにする*/

//disableinvert;カーソルキーが表示されなくなるためコメントアウトした。

call main getarg(1), getarg(2), getarg(3);
endmacro str(##return);


main:
	call ShowPerformanceCounter "main";
	##old_column=column;
	##old_lineno=lineno;

	call ParseOptions $$2/*options*/;
	if(! ##return){
		return false;
	}
	call ShowPerformanceCounter "ParseOptions";

	call LoadConfigIni;
	call ShowPerformanceCounter "LoadConfigIni";

	call Start $$1/*sources*/, $$3/*sources_args*/;
	##ret = ##return;

	//
	//マクロ終了時に呼び出すコールバックを集める
	//
	##callback_size=0;
	if(#g_dll_ohtorii_tools){
		##callback_size=dllfuncw(#g_dll_ohtorii_tools,"GetCallBack_ProcessFinish_Size");
		##i=0;
		while(##i<##callback_size){
			$$callback_macro_filename[##i]	=dllfuncstrw(#g_dll_ohtorii_tools,"GetCallBack_ProcessFinish_MacroFilename",##i);
			$$callback_label[##i]			=dllfuncstrw(#g_dll_ohtorii_tools,"GetCallBack_ProcessFinish_LabelName",##i);
			$$callback_args[##i]			=dllfuncstrw(#g_dll_ohtorii_tools,"GetCallBack_ProcessFinish_Args",##i);

			##i = ##i + 1;
		}

	}

	//カーソル位置を元に戻す
	moveto2 ##old_column,##old_lineno;

	//
	//後処理
	//
	call FreeDll;
	call CloseFileIfOpen;

	//マクロ終了時に呼び出すコールバックを実際に呼ぶ
	##i=0;
	while(##i<##callback_size){
		execmacro $$callback_macro_filename[##i], $$callback_label[##i], $$callback_args[##i];
		##i = ##i + 1;
	}
	return ##ret;


/*オプションをディフォルト値で初期化
*/
InitializeOptions:
	#g_option_auto_preview=false;
	$g_option_default_action="";
	return ;


ParseOptions:
	execmacro currentmacrodirectory+"\\create_temp_file.mac";
	$$ini_filename=getresultex(-1);
	if($$ini_filename==""){
		return false;
	}
	$$section="options";
	call WriteToFileUnicode $$ini_filename, "["+$$section+"]\n"+$$1;
	if(! ##return){
		return false;
	}

	$$auto_preview	=getinistrw($$ini_filename,$$section,"auto-preview");
	$$default_action=getinistrw($$ini_filename,$$section,"default-action");

	call InitializeOptions;
	call StringToBool $$auto_preview;
	#g_option_auto_preview 		= ##return;
	$g_option_default_action 	= $$default_action;

	deletefile $$ini_filename;
	return true;


StringToBool:
	$$s = tolower($$1);
	if($$s=="on"){
		return true;
	}
	if($$s=="true"){
		return true;
	}
	if($$s=="1"){
		return true;
	}

	if($$s=="off"){
		return false;
	}
	if($$s=="false"){
		return false;
	}
	if($$s=="0"){
		return false;
	}
	return false;


LoadConfigIni:
	$$ini_filename=$g_root_macro_directory+"\\config.ini";
	#keyinput_interval = getininumw($$ini_filename, "unity", "keyinput_interval");
	if(#keyinput_interval <= 0){
		#keyinput_interval=80;
	}
	return ;


/*
return	アクションが最後まで実行されたかどうか(bool)
		false	実行された
		true	実行されていない
*/
Start:
	if(0){
		//debug
		$$source_list="emoji";
	}else{
		$$source_list=$$1;
	}

	if($$source_list==""){
		return false;
	}
	$$sources_args=$$2;
	call ShowPerformanceCounter "Start";

	call NewFile;
	if(! ##return){
		return false;
	}
	call ShowPerformanceCounter "NewFile";

	call LoadDll;
	if(! ##return){
		return false;
	}
	call ShowPerformanceCounter "LoadDLL";

	##ret = dllfuncw(#g_dll_ohtorii_tools,"LogEnable",false);
	##ret = dllfuncw(#g_dll_ohtorii_tools,"StaticStatusInitialize",#g_target_hidemaru,#g_new_hidemaru,directory2,$g_root_macro_directory);


	##ret=dllfuncw(#g_dll_ohtorii_tools,"KindsLoadAll",$g_root_macro_directory+"\\kinds\\");
	if(! ##ret){
		return false;
	}
	call ShowPerformanceCounter "LoadKindAll";

	##ret=dllfuncw(#g_dll_ohtorii_tools,"SourcesLoadAll",$g_root_macro_directory+"\\sources\\");
	if(! ##ret){
		return false;
	}
	call ShowPerformanceCounter "LoadSourceAll";

	{//ソースを読み込む
		##num_source     =split($$sources,    $$source_list, "\t");
		##num_source_args=split($$source_args,$$sources_args,"\n");
		##i = 0;
		while(##i < ##num_source){
			$$source_filename=$g_root_macro_directory+"\\sources\\"+$$sources[##i]+".mac";

			$$args[0]	=str(#g_dll_ohtorii_tools);
			$$args[1]	=str(#g_dll_hm_process) ;
			$$args[2]	=$$source_filename;

			/*メモ：##iが配列の範囲外の場合は空白文字になるため、明示的な##iの範囲確認は省略した*/
			$$args[3]	=$$source_args[##i];

			$$args[30]	="";
			execmacro currentmacrodirectory+"\\source.mac", $$args, 31;
			if(getresultex(-1) != "1"){
				return 0;
			}
			##i = ##i + 1;
		}
	}
	call ShowPerformanceCounter "ソースの読み込み";

	call RefineSearch "", "", #g_candidate_mode_text;
	return ##return;


//#region 絞り込み検索
RefineSearch:
	/*
	引数	$$1		ソースのファイル名
			$$2		引数
			##3		候補モード(#g_candidate_mode_*)

	return	#g_result_*;
	*/
	$$source_filename	= $$1;
	$$source_args		= $$2;
	##candidate_mode	= ##3;

	//
	//ソースから候補を生成する
	//
	if($$source_filename!=""){
		$$args[0]	=str(#g_dll_ohtorii_tools);
		$$args[1]	=str(#g_dll_hm_process) ;
		$$args[2]	=$$source_filename;
		$$args[3]	=$$source_args;
		$$args[30]	="";
		execmacro currentmacrodirectory+"\\source.mac", $$args, 31;
		if(getresultex(-1)!="1"){
			return #g_result_faile;
		}
	}

	//絞り込み検索を行う
	call RefineSearchMain ##candidate_mode;
	##ret=##return;
	return ##ret;


PushRefineSearch:
	##exist_context_then_delete=true;
	##ret=dllfuncw(#g_dll_ohtorii_tools,"PushContext", ##exist_context_then_delete);
	return ##ret;


PopRefineSearch:
	##exist_context_then_delete=true;
	##ret=dllfuncw(#g_dll_ohtorii_tools,"PopContext", ##exist_context_then_delete);
	return ##ret;


RefineSearchMain:
	/*
	引数	##1		候補モード
	return	#g_result_*;
	*/
	##candidate_mode=##1;
	$$keybord_buffer="";

	execmacro $g_command_line_path+"\\parse_result.mac" , "cursor"		, $$keybord_buffer;
	$$cursor_pos=getresultex(-1);
	execmacro $g_command_line_path+"\\parse_result.mac" , "keybuffer"	, $$keybord_buffer;
	$$key_buffer=getresultex(-1);

	deletecolormarkerall $g_color_mark;

	//事前に実行してDLL側のデータを作る。
	call Filter $$key_buffer;

	$$prev_pattern						="999";
	##candidates_update_interval		=20;
	##candidates_update_prev_tickcount	=0;

	##hm_process_update_interval		=100;
	##hm_process_update_prev_tickcount	=0;

	##auto_update_interval				=200;
	##auto_update_prev_tickcount		=0;

	while(true){
		execmacro $g_command_line_path+"\\input-keys.mac", $$key_buffer, $$cursor_pos;
		$$keybord_buffer=getresultex(-1);
		execmacro $g_command_line_path+"\\parse_result.mac" , "keycode"		, $$keybord_buffer;
		$$keycode=getresultex(-1);
		execmacro $g_command_line_path+"\\parse_result.mac" , "cursor"		, $$keybord_buffer;
		$$cursor_pos=getresultex(-1);
		execmacro $g_command_line_path+"\\parse_result.mac" , "keybuffer"	, $$keybord_buffer;
		$$key_buffer=getresultex(-1);

		call UpdateKeyStatus;
		##ctrl 	=iskeydown(0x11);
		##q		=iskeydown(0x51);

		if(0){/*デバッグ*/
			if($$keycode!="0"){
				debuginfo(sprintf("$$keycode=%s / ctrl=%d",$$keycode,##ctrl));
			}
		}

		if($$keycode=="13"){													/*Enter / ctrl-m*/
			##old_column=column;
			##old_lineno=lineno;

			call Enter ##candidate_mode;
			if(##return==#g_result_continue){
				call Continue $$key_buffer;

				//カーソル位置を元に戻す
				moveto2 ##old_column,##old_lineno;
				call CursorMoveCommon 0;

				continue;
			}
			if(##return==#g_result_quit){return #g_result_success;}
			return ##return;
		}else if($$keycode=="27")				{return #g_result_cancel;}		//Escape
		else if($$keycode=="16")				{call CursorUp;}				//ctrl-p
		else if($$keycode=="14")				{call CursorDown;}				//ctrl-n
		else if(##ctrl && ($$keycode=="32"))	{call Mark;call CursorDown;}	//ctrl-space
		else if(##ctrl && ($$keycode=="1"))		{call MarkAll;}					//ctrl-a
		else if($$keycode == "29")				{call CursorUp;}				//↑
		else if($$keycode == "31")				{call CursorDown;}				//↓
		else if(#g_keyprss_page_up)				{call PageUp;}					//PageUp
		else if(#g_keyprss_page_down)			{call PageDown;}				//PageDown
		else if(##ctrl && #g_keyprss_home)		{call GoFileTop;}				//ctrl-home
		else if(##ctrl && #g_keyprss_end)		{call GoFileEnd;}				//ctrl-end
		else if(($$keycode=="3") || (##ctrl&&##q))	{return #g_result_quit;}	//ctrl-c / ctrl-q
		else if($$keycode=="23")				{call SeparateWindow;}			//ctrl-w
		else if($$keycode=="9"){												//tab / ctrl-i
			/*	アクションを選択する */
			##old_column=column;
			##old_lineno=lineno;

			##current_context=dllfuncw(#g_dll_ohtorii_tools,"GetCurrentContext");
			call NewCandidate currentmacrodirectory+"\\generate_action_candidates.mac",str(##current_context),#g_candidate_mode_action;
			if(##return==#g_result_continue){
				if(! ##return){
					return #g_result_faile;
				}
				call Continue $$key_buffer;

				//カーソル位置を元に戻す
				moveto2 ##old_column,##old_lineno;
				call CursorMoveCommon 0;

				continue;
			}
			if(##return==#g_result_quit)	{return #g_result_success;}
			return ##return;
		}else{
			//候補の更新
			##canged_candidates=dllfuncw(#g_dll_ohtorii_tools,"ClearChangedCandidatesAndReturnPrevStatus");
			if((##canged_candidates) || (($$prev_pattern != $$key_buffer) && (##candidates_update_interval<(tickcount-##candidates_update_prev_tickcount)))){
				##candidates_update_prev_tickcount=tickcount;
				call Filter $$key_buffer;
				$$prev_pattern=$$key_buffer;
			}
			//プロセスの更新
			if(##hm_process_update_interval < (tickcount-##hm_process_update_prev_tickcount)){
				##hm_process_update_prev_tickcount=tickcount;
				call UpdateHmPocess;
			}
		}

		if(#g_option_auto_preview){
			//auto_updateの更新
			if(##auto_update_interval < (tickcount-##auto_update_prev_tickcount)){
				##auto_update_prev_tickcount=tickcount;
				call DoAutoPreview;
			}
		}

		//定期処理を呼び出す
		##ret=dllfuncw(#g_dll_ohtorii_tools,"RecurringTask");
	}
	return #g_result_faile;


Enter:
	/*Enterキーの処理

	return	#g_result_*
	*/
	##candidate_mode=##1;

	$$args[0]	=str(#g_dll_ohtorii_tools);
	$$args[1]	=str(#g_dll_hm_process) ;
	$$args[30]	=$g_option_default_action;
	execmacro currentmacrodirectory+"\\action.mac", $$args, 31;
	if(getresultex(-1) != "1"){
		return #g_result_faile;		//失敗
	}

	//
	//成功
	//

	##is_quit=dllfuncw(#g_dll_ohtorii_tools,"StaticStatusGetIsQuit");
	if(##is_quit){
		return #g_result_quit;
	}

	##is_start=dllfuncw(#g_dll_ohtorii_tools,"StaticStatusEnableIsStart");
	if(##is_start){
		/*memo　候補の再取得について。
		NewCandidate　中で候補の再取得が行われるため、このif文中では何もしない。
		*/
		$$source_name=dllfuncstrw(#g_dll_ohtorii_tools,"StaticStatusGetIsStartSourceName");
		$$arg=dllfuncstrw(#g_dll_ohtorii_tools,"StaticStatusGetIsStartArg");
		$$source_filename=dllfuncstrw(#g_dll_ohtorii_tools,"SourcesSourceNameToFileName",$$source_name);
		$$source_filename=$$source_filename+".mac";
		call NewCandidate, $$source_filename, $$arg, ##candidate_mode;
		return ##return;
	}else{
		if(##candidate_mode==#g_candidate_mode_text){
			//ディフォルトアクションの実行後
			call GatherRegetCandidates;
			$$source_arg="";	//todo 後で実装
			call TryRegetCandidates $$source_arg;
		}
	}
	return #g_result_continue;


Continue:
	/*画面を更新して再度絞り込み検索を行う。
	$$1 	key_buffer
	*/
	call Filter $$1;
	return ;

NewCandidate:
	/*新規の候補選択

	（引数）	$$1		ソースのファイル名
				$$2		ソースの引数
				##3		候補モード

	return	#g_result_quit
			#g_result_faile
			#g_result_continue
	*/
	debuginfo "Enter NewCandidate";
	$$source_filename	=$$1;
	$$source_arg		=$$2;
	##candidate_mode	=##3;

	debuginfo "  $$source_filename="+$$source_filename;
	debuginfo "  $$source_arg="+$$source_arg;

	call PushRefineSearch;
	if(##return==false){
		//(Memo)TABを連何度も押すとこの処理に入る。とりあえず無視する。
		debuginfo "  [Failed]Push";
		return #g_result_continue;
	}

	call RefineSearch $$source_filename	,$$source_arg,##candidate_mode;
	##search_result	=##return;
	##is_quit		=dllfuncw(#g_dll_ohtorii_tools,"StaticStatusGetIsQuit");
	call GatherRegetCandidates;

	call PopRefineSearch;
	if(##return==false){
		debuginfo "  [Failed]Pop";
		return #g_result_faile;
	}

	if(##candidate_mode==#g_candidate_mode_action){
		//アクション実行後の状態なので候補の更新を試す。
		call TryRegetCandidates $$source_arg;
	}
	if(##search_result==#g_result_cancel){
		//キャンセル
		debuginfo "   cancel.";
		return #g_result_continue;
	}
	if(##search_result==#g_result_success){
		//成功
		if(##is_quit){
			debuginfo "    quit";
			return #g_result_quit;
		}
		return #g_result_continue;
	}
	return ##search_result;


/*候補の再取得に必要なソース名を集める*/
GatherRegetCandidates:
	$g_reget_sourcenames[0]="";
	#g_reget_sourcenames_count=dllfuncw(#g_dll_ohtorii_tools,"StaticStatusGetNumberOfSourceNamesForReacquisitionCandidates");
	##i=0;
	while(##i<#g_reget_sourcenames_count){
		$g_reget_sourcenames[##i] = dllfuncstrw(#g_dll_ohtorii_tools,"StaticStatusGetSourceNameForReacquisitionCandidates", ##i);
		##i = ##i + 1;
	}
	##ret=dllfuncw(#g_dll_ohtorii_tools,"StaticStatusClearSourceNamesForReacquisitionCandidates");
	return ;


/*候補の再取得を試みる
*/
TryRegetCandidates:
	debuginfo "==== TryRegetCandidates ====";
	debuginfo "    #g_reget_sourcenames_count="+str(#g_reget_sourcenames_count);
	if(#g_reget_sourcenames_count==0){
		return ;
	}
	$$source_arg=$$1;

	##source_name_index=0;
	while(##source_name_index < #g_reget_sourcenames_count){
		$$reget_source_filename = dllfuncstrw(#g_dll_ohtorii_tools,"SourcesSourceNameToFileName",$g_reget_sourcenames[##source_name_index]) + ".mac";
		$$args[0]	=str(#g_dll_ohtorii_tools);
		$$args[1]	=str(#g_dll_hm_process) ;
		$$args[2]	=$$reget_source_filename;
		$$args[3]	=$$source_arg;
		$$args[30]	="reget";
		execmacro currentmacrodirectory+"\\source.mac", $$args, 31;
		if(getresultex(-1)!="1"){
			return false;
		}

		##source_name_index = ##source_name_index + 1;
	}
	return ;


UpdateKeyStatus:
	/*キー入力の処理

	(memo)
	・キーリピートを自力で処理しています。
	・見ての通りやっつけ仕事です。
	*/
	#g_keyprss_page_up  =false;
	#g_keyprss_page_down=false;
	#g_keyprss_home		=false;
	#g_keyprss_end		=false;

	$$variable_name = "unity_keydown_tickcount";
	##share=0;

	##prev = val(getstaticvariable($$variable_name, ##share ));
	##diff = tickcount - ##prev;
	if(##diff < #keyinput_interval){
		return false;
	}

	#g_keyprss_page_up  =iskeydown(0x21);
	#g_keyprss_page_down=iskeydown(0x22);
	#g_keyprss_home		=iskeydown(0x24);
	#g_keyprss_end		=iskeydown(0x23);
	if(	#g_keyprss_page_up	||
		#g_keyprss_page_down||
		#g_keyprss_home		||
		#g_keyprss_end
	){
		setstaticvariable $$variable_name, str(tickcount), ##share;
		return true;
	}
	return false;


CursorMoveCommon:
	##delta = ##1;
	##next_line_no=dllfuncw(#g_dll_ohtorii_tools,"RefineSearchMoveHidemaruCursorLineNo",lineno,##delta);
	if(##next_line_no == -1){
		return false;
	}
	//実際にカーソル位置を移動する
	moveto2 column,##next_line_no;

	//秀丸エディタ側の実際のカーソル位置を反映して、
	//DLL側と秀丸エディタ側のカーソル位置を一致させる
	##n=dllfuncw(#g_dll_ohtorii_tools,"RefineSearchSetHidemaruLineno",lineno);
	return ##n;


GoFileTop:
	gofiletop;
	golinetop2;
	call CursorMoveCommon 0;
	return ##return;

GoFileEnd:
	gofileend;
	golinetop2;
	call CursorMoveCommon 0;
	return ##return;

CursorUp:
	call CursorMoveCommon -1;
	return ##return;


CursorDown:
	call CursorMoveCommon 1;
	return ##return;

PageUp:
	halfprevpage;
	call CursorMoveCommon 0;
	return;

PageDown:
	halfnextpage;
	call CursorMoveCommon 0;
	return ;

SeparateWindow:
	if(! tabmode){
		return ;
	}
	if(tabtotal <= 1){
		return ;
	}
	settabgroup hidemaruhandle(0),-1;
	return ;

MarkAll:
	//全行をマークする
	disabledraw;
	##old_column=column;
	##old_lineno=lineno;

	##line=1;
	##count=linecount2;
	while(##line <= ##count){
		moveto2 ##old_column,##line;
		##ret=dllfunc(#g_dll_ohtorii_tools,"RefineSearchChangeMarked",lineno,true);
		selectline 0;
		call ColorMarker;
		escape;

		##line = ##line + 1;
	}

	//カーソル位置を元に戻す
	moveto2 ##old_column,##old_lineno;

	enabledraw;
	return;


Mark:
	//カーソル行に色を塗る
	disabledraw;
	$$marker=getcolormarker(0x0001,$g_color_mark);
	if($$marker=="000000FF"){
		//マーク削除
		##ret=dllfunc(#g_dll_ohtorii_tools,"RefineSearchChangeMarked",lineno,false);
		selectline 0;
		deletecolormarker $g_color_mark;
		escape;
	}else{
		//マークする
		##ret=dllfunc(#g_dll_ohtorii_tools,"RefineSearchChangeMarked",lineno,true);
		selectline 0;
		call ColorMarker;
		escape;
	}

	enabledraw;
	return;


Filter:
	/*絞り込み検索を行う
	*/
	call TrimString $$1;
	$$pattern=$$return;

	##old_lineno=lineno;

	disabledraw;
	selectall;
	//backspace;
	##ret=dllfuncw(#g_dll_ohtorii_tools,"RefineSearchDo",$$pattern);
	insert(dllfuncstrw(#g_dll_ohtorii_tools,"RefineSearchGetResult"));
	call HilightWorld $$pattern;

	//
	//選択行をマークする
	//
	##i = 0;
	##count = dllfuncw(#g_dll_ohtorii_tools,"RefineSearchGetMarkedCount");
	while(##i<##count){
		##selected_lineno = dllfuncw(#g_dll_ohtorii_tools,"RefineSearchConvertSelectedIndexToHidemaruLineno",##i);
		if(0 < ##selected_lineno){
			movetolineno 1, ##selected_lineno;
			selectline 0;
			call ColorMarker;
			escape;
		}
		##i = ##i + 1;
	}

	//
	//行位置を維持する対策。
	//
	//まずは、行数が増減するためカーソル位置を前候補の位置に移動する
	moveto2 0,##old_lineno;

	//次に正しい行番号へ移動する。(そのためにdelta=0としている)
	call CursorMoveCommon 0;
	if(! ##return){
		//処理失敗==ヘッダ部、なので一つ下(delta=1)へ移動する。
		call CursorMoveCommon 1;
	}

	refreshtabstop;
	enabledraw;
	return result;


ColorMarker:
	colormarker 0x000000ff,-1,-1,0,0x00,$g_color_mark;
	return ;


HilightWorld:
	/*
	検索文字列をハイライト表示する
	*/

	$$search_words=$$1;
	call SearchWordsToHidemaruRegex $$search_words;
	$$regex=$$return;
	setsearch $$regex,0x00000800|0x00000010;
	hilightfound 1;
	return;

SearchWordsToHidemaruRegex:
	/*空白区切りの文字列を秀丸エディタの正規表現に変換する。
	[in]	"hoge 123 .txt"
	[out]	"hoge|123|\.txt"
	*/
	$$word_list=$$1;
	$$escaped=quote($$word_list);
	$$regex = dllfuncstr(#g_hmjre_dll,"ReplaceRegular", "[" + $g_space + "]+", $$escaped, 0, "|", 2);
	return $$regex;

UpdateHmPocess:
	$$text=dllfuncstrw(#g_dll_hm_process,"ReadStandardOutputAll");
	if($$text!=""){
		call AppendTextToOutputPanel $$text;
	}
	$$text=dllfuncstrw(#g_dll_hm_process,"ReadStandardErrorAll");
	if($$text!=""){
		call AppendTextToOutputPanel $$text;
	}
	return ;


DoAutoPreview:
	##first_item_index	= 0;
	$$text=dllfuncstrw(#g_dll_ohtorii_tools,"GetSelectionText",##first_item_index);
	if($$text==""){
		return ;
	}
	if($g_option_auto_preview_context_candidate==$$text){
		return ;
	}

	$$args[0]	=str(#g_dll_ohtorii_tools);
	$$args[1]	=str(#g_dll_hm_process) ;
	$$args[30]	="preview";
	execmacro currentmacrodirectory+"\\action.mac", $$args, 31;
	$g_option_auto_preview_context_candidate=$$text;
	return ;


AppendTextToOutputPanel:
	if(#g_dll_output_panel==0){
		#g_dll_output_panel=loaddll("HmOutputPane.dll");
		if(#g_dll_output_panel==0){
			return false;
		}
	}
	##ret=dllfunc(#g_dll_output_panel,"Output",hidemaruhandle(0),$$1);
	return ##ret;


TrimString:
	if($$2==""){
		$$space=$g_space;
	}else{
		$$space=$$2;
	}
	while(1){
	  ##word=strlen($$1);
	  //文字列の先頭に空白文字がある場合、空白文字を削除
	  if(strstr($$space,leftstr($$1,2))!=-1)$$1=rightstr($$1,##word-2);
	  else if(strstr($$space,rightstr($$1,2))!=-1)$$1=leftstr($$1,##word-2);
	  //ここまで全角文字の対処
	  else if(strstr($$space,leftstr($$1,1))!=-1)$$1=rightstr($$1,##word-1);
	  else if(strstr($$space,rightstr($$1,1))!=-1)$$1=leftstr($$1,##word-1);
	  else break;
	}
	return $$1;

//#region 秀丸の編集画面
NewFile:
	newfile;
	if(result==0){
		#g_open_newfile=false;
		return false;
	}

	#g_open_newfile=true;
	#g_new_hidemaru=hidemaruhandle(0);
	if(false){
		/* 折りたたみを表示しない*/
		config "xFoldingTwigBar:0";
		/*行番号表示*/
		config "xShowLineNo:0";
	}
	/*見出しバー表示*/
	config "xOutlineBar:1";
	/*TSVモード*/
	config "xTabMode:0x0001";
	config "xGuideLine:0x00";
	config "xOrikaeshiLine:0";

	setcompatiblemode(compatiblemode|
	                    0x00020000  |	/*マクロ終了後に検索条件をマクロ実行前と同じに戻します。（V8.20以降） */
						0x02000000		/*カーソル行の下線やカーソル位置の縦線の描画を有効にします。（V8.90β6以降）*/
					);

	return true;

CloseFileIfOpen:
	if(#g_open_newfile){
		setactivehidemaru	#g_target_hidemaru;
		closehidemaruforced #g_new_hidemaru;

		#g_target_hidemaru=0;
		#g_new_hidemaru=0;

		#g_open_newfile=false;
	}
	return true;


/*文字列をファイルへ書き込む
$$1	ファイル名
$$2	ファイルへ書き込む文字列
*/
WriteToFileUnicode:
    ##objFso = createobject("Scripting.FileSystemObject");
    ##objFile = member(##objFso,"OpenTextFile"
    				,$$1	//ファイル名
			        , 2     //ForWriting
			        , 1     //Create=True
			        , -1    //Unicode
			        );
    member ##objFile,"WriteLine",$$2;
    member ##objFile,"Close";
    releaseobject ##objFile;
    releaseobject ##objFso;
    return true;


//#region DLL
LoadDll:
	execmacro currentmacrodirectory+"\\load_ohtorii_tools_dll.mac";
	#g_dll_ohtorii_tools=val(getresultex(-1));
	if (#g_dll_ohtorii_tools==0) {
		return false;
	}

	execmacro currentmacrodirectory+"\\load_hm_process_dll.mac";
	#g_dll_hm_process=val(getresultex(-1));
	if (#g_dll_hm_process==0) {
		return false;
	}

	execmacro currentmacrodirectory+"\\load_dll.mac", "HmJre.dll";
	#g_hmjre_dll=val(getresultex(-1));
	if(#g_hmjre_dll==0){
		return false;
	}
	return true;


FreeDll:
	if(#g_dll_ohtorii_tools!=0){
		freedll #g_dll_ohtorii_tools;
		#g_dll_ohtorii_tools=0;
	}
	if(#g_dll_hm_process!=0){
		##ret = dllfuncw(#g_dll_hm_process,"Finish");
		freedll #g_dll_hm_process;
		#g_dll_hm_process=0;
	}

	if(#g_hmjre_dll!=0){
		freedll #g_hmjre_dll;
		#g_hmjre_dll=0;
	}

	if(#g_dll_output_panel!=0){
		freedll #g_dll_output_panel;
		#g_dll_output_panel=0;
	}
	return true;


//#region そのた
ShowPerformanceCounter:
	##diff=tickcount-#g_tick_prev;
	debuginfo sprintf("[%d]%s",##diff,$$1);
	#g_tick_prev=tickcount;
	return ;
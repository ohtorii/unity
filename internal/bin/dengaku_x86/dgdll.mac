//	田楽(でんがく)ＤＬＬ テスト用秀丸マクロ
//	2006/05/21 written by M. Sugiura

//	最初の比較式は「浮動小数点数版秀丸」の場合 1/2 == 0.5 になるので偽になる
if (1/2 == 0 && version < 302) {
	message "このマクロには秀丸の Ver.3.02 以降が必要です。";
	endmacro;
}

//	田楽ＤＬＬのロード
loaddll hidemarudir + "\\DengakuDLL.dll";
if (!result) {
	message "DLL をロードできませんでした。";
	endmacro;
}

//	ＤＬＬのバージョンチェック
$DLLVer = dllfuncstr("GETVERSION");
if (val(leftstr($DLLVer, 1)) < 2) {
	message "このマクロの実行には Ver.2.00 以降の田楽ＤＬＬが必要です。";
	freedll;
	endmacro;
}

//	ポップアップメニューの構築
//	※一度登録しておけば freedll を行うまで有効

//	メインメニュー
##r = dllfunc("NEWMENU", "main");
if (##r == 0) {
	message "main メニューの作成に失敗しました。";
	goto ERROR;
}
if (dllfunc("ADDMENUITEM","","(&F)\tコモンダイアログの表示","comdlg") == 0) {
	message "main メニューへの項目1の追加に失敗しました。";
	goto ERROR;
}
if (dllfunc("ADDMENUITEM","","(&C)\tダイアログ/コントロール","dialog") == 0) {
	message "main メニューへの項目2の追加に失敗しました。";
	goto ERROR;
}
if (dllfunc("ADDMENUITEM","","","") == 0) {	//	セパレータ
	message "main メニューへの項目3の追加に失敗しました。";
	goto ERROR;
}
if (dllfunc("ADDMENUITEM","","(&Q)\t終了","") == 0) {
	message "main メニューへの項目4の追加に失敗しました。";
	goto ERROR;
}

//	コモンダイアログ関係のサブメニュー
if (dllfunc("NEWMENU","comdlg") == 0 ||
	dllfunc("ADDMENUITEM","","(&F)\t「ファイルを開く」ダイアログのテスト","") == 0 ||
	dllfunc("ADDMENUITEM","","(&D)\t「フォルダの指定」ダイアログのテスト","") == 0 ||
	dllfunc("ADDMENUITEM","","(&E)\t「フォルダの指定」ダイアログのテスト(フォルダ操作が可能)","") == 0 ||
	dllfunc("ADDMENUITEM","","(&C)\t「色の指定」ダイアログのテスト","") == 0 ||
	dllfunc("ADDMENUITEM","","(&M)\t「フォントの指定」ダイアログのテスト","") == 0) {
	message "comdlg メニューの作成に失敗しました。";
	goto ERROR;
}

//	ダイアログ関係のサブメニュー
if (dllfunc("NEWMENU","dialog") == 0 ||
	dllfunc("ADDMENUITEM","","(&S)\tフォント/状態/タイトルの変更","") == 0 ||
	dllfunc("ADDMENUITEM","","(&F)\tフォントの装飾の変更","") == 0 ||
	dllfunc("ADDMENUITEM","","(&C)\t各種コントロール","control") == 0) {
	message "dialog メニューの作成に失敗しました。";
	goto ERROR;
}

//	各種コントロール関係のサブサブメニュー
if (dllfunc("NEWMENU","control") == 0 ||
	dllfunc("ADDMENUITEM","","(&M)\tマクロサーバ互換のコントロール","") == 0 ||
	dllfunc("ADDMENUITEM","","(&R)\t「参照」ボタン","") == 0 ||
	dllfunc("ADDMENUITEM","","(&B)\tコンボボックス・ドロップダウンリスト","") == 0 ||
	dllfunc("ADDMENUITEM","","(&S)\tスピンボタン","") == 0 ||
	dllfunc("ADDMENUITEM","","(&T)\tトラックバー","") == 0 ||
	dllfunc("ADDMENUITEM","","(&L)\tリストビュー","") == 0 ||
	dllfunc("ADDMENUITEM","","(&C)\tチェックリスト","") == 0 ||
	dllfunc("ADDMENUITEM","","(&E)\t拡張エディットボックス","") == 0 ||
	dllfunc("ADDMENUITEM","","(&F)\tフレーム","") == 0 ||
	dllfunc("ADDMENUITEM","","(&T)\tタブ","") == 0 ||
	dllfunc("ADDMENUITEM","","(&G)\tグループボックス","") == 0 ||
	dllfunc("ADDMENUITEM","","(&V)\tツリービュー","") == 0) {
	message "control メニューの作成に失敗しました。";
	goto ERROR;
}

//	メインルーチン
MAIN:
//	メニューの表示（現在のカーソル位置に表示）
$item = dllfuncstr("MENUEX", "main", hidemaruhandle(0), 0, 3 * fontsize);
if ($item == "!") goto ERROR;
else if ($item == "0" || $item == "main:4") goto END;	//	マクロの終了
else if (leftstr($item,6) == "comdlg") {
	//	コモンダイアログのテスト
	##i = val(midstr($item,7,2));
	if (##i == 1) call FILEOPEN;
	else if (##i == 2) call DIROPEN;
	else if (##i == 3) call DIROPENEX;
	else if (##i == 4) call COLOROPEN;
	else call FONTOPEN;
} else if (leftstr($item,6) == "dialog") {
	//	ダイアログのテスト
	##i = val(midstr($item,7,2));
	if (##i == 1) call SETSTATE;
	else call SETFONT;
} else if (leftstr($item,7) == "control") {
	//	コントロールのテスト
	##i = val(midstr($item,8,2));
	if (##i == 1) call MACSRV;
	else if (##i == 2) call REFBUTTON;
	else if (##i == 3) call COMBOBOX;
	else if (##i == 4) call SPINBUTTON;
	else if (##i == 5) call TRACKBAR;
	else if (##i == 6) call LISTVIEW;
	else if (##i == 7) call CHECKLIST;
	else if (##i == 8) call MLEDIT;
	else if (##i == 9) call FRAME;
	else if (##i == 10) call TAB;
	else if (##i == 11) call GROUP;
	else if (##i == 12) call TREE;
}
goto MAIN;	//	ループ動作

MAKEDLG_ERROR:
message "ダイアログの作成に失敗しました。";
goto END;

ERROR:
message "何らかのエラーによりマクロを中止します。";

END:
freedll;
endmacro;


FILEOPEN:
//	「ファイルを開く」ダイアログのテスト
$name = dllfuncstr("GETFILENAME",
			hidemaruhandle(0),	//	オーナーウィンドウのウィンドウハンドル
			"「ファイルを開く」ダイアログのテスト",	//	タイトル
			hidemarudir,	//	初期フォルダ名（ここでは秀丸フォルダを指定）
			"'実行ファイル(*.exe;*.com;*.bat)' 'テキストファイル(*.txt)'"
			//	「ファイルの種類」コンボボックスの内容
		);
if ($name == "") message "キャンセルされました。";
else {
	if (strstr($name,"|") != -1) {	//	ファイルが複数選択された場合
		//	$name = "dirname|basename1|basename2|..." を
		//	「dirname 中の
		//	   basename1
		//	   basename2
		//	   ...」に変換
		$name = dllfuncstr("GSUB",$name,"|"," 中の\n ",1);
		if (strstr($name,"|") != -1)
			$name = dllfuncstr("GSUB",$name,"|","\n ",-1);
	}
	message "選択されたファイルは\n" + $name + "\nです。";
}
return;


DIROPEN:
//	「フォルダの指定」ダイアログのテスト
$name = dllfuncstr("GETDIRNAME",
			hidemaruhandle(0),	//	オーナーウィンドウのウィンドウハンドル
			"「フォルダの指定」ダイアログのテスト",	//	タイトル
			hidemarudir	//	初期フォルダ名（ここでは秀丸フォルダを指定）
		);
if ($name == "") message "キャンセルされました。";
else message "選択されたフォルダは\n" + $name + "\nです。";
return;


DIROPENEX:
//	「フォルダの指定」ダイアログのテスト
$name = dllfuncstr("GETDIRNAMEEX",
			hidemaruhandle(0),
			"「フォルダの指定」ダイアログのテスト",
			hidemarudir,
			1	//	フォルダの作成等を許可する
		);
if ($name == "") message "キャンセルされました。";
else message "選択されたフォルダは\n" + $name + "\nです。";
return;


COLOROPEN:
//	「色の指定」ダイアログのテスト
$name = dllfuncstr("GETCOLOR",
			hidemaruhandle(0),	//	オーナーウィンドウのウィンドウハンドル
			"「色の指定」ダイアログのテスト",	//	タイトル
			"#FF0000#00FF00#0000FF"	//	ユーザー定義色の指定
		);
if ($name == "") message "キャンセルされました。";
else {
	if (strlen($name) > 7) {	//	ユーザー定義色があった場合
		$$msg = leftstr($name,7) + " で、\n作成された色は";
		while (1) {
			$name = midstr($name,7,112); // ユーザー定義色は最大 16 個
			if (strlen($name) <= 0) break;
			$$msg = $$msg + "\n " + leftstr($name,7);
		}
		$name = $$msg;
	}
	message "選択された色は\n " + $name + "\nです。";
}
return;


FONTOPEN:
//	「フォントの指定」ダイアログのテスト
$name = dllfuncstr("GETFONT",
			hidemaruhandle(0),	//	オーナーウィンドウのウィンドウハンドル
			"「フォントの指定」ダイアログのテスト",	//	タイトル
			"Terminal 10 red biu"	//	初期フォント指定
		);
if ($name == "") message "キャンセルされました。";
else {
	//	$name = "Terminal,10,#FF0000,biu" という形式で返ってくる
	$$fontface = dllfuncstr("GETTOKEN",$name,",");
	$$fontsize = dllfuncstr("GETTOKEN","",",");
	$$fontcolor = dllfuncstr("GETTOKEN","",",");
	$$ftflag = dllfuncstr("GETTOKEN","",",");
	$$fonttype = "";
	if ($$ftflag != "") {
		if (strstr($$ftflag,"bi") != -1) $$fonttype = "ボールドイタリック体";
		else if (strstr($$ftflag,"b") != -1) $$fonttype = "ボールド体";
		else if (strstr($$ftflag,"i") != -1) $$fonttype = "イタリック体";
		else $$fonttype = "標準";
		if (strstr($$ftflag,"u") != -1) {	//	下線あり
			if ($$fonttype != "") $$fonttype = $$fonttype + ", ";
			$$fonttype = $$fonttype + "下線";
		}
		if (strstr($$ftflag,"s") != -1) {	//	打ち消し線あり
			if ($$fonttype != "") $$fonttype = $$fonttype + ", ";
			$$fonttype = $$fonttype + "打ち消し線";
		}
	}
	message "選択されたフォントは\n フォント名：\t" + $$fontface + 
			"\n フォントサイズ：\t" + $$fontsize + 
			" pt\n フォントの色：\t" + $$fontcolor + 
			"\n フォントの種類：\t" + $$fonttype + "\nです。";
}
return;


SETSTATE:
//	状態変更のテスト
if (dllfunc("NEWDIALOG",
			"フォント/状態/タイトル変更のテスト",	//	タイトル
			40,	//	横幅の初期値(文字単位)
			"0 0 center 'ＭＳ ゴシック' 11"	//	表示位置その他のオプション
	) == 0 ||
	dllfunc("NEWCONTROL","text","focus","フォーカスの場所：") == 0 ||
	dllfunc("NEWCONTROL","button","btn1","有効/無効の切替") == 0 ||
	dllfunc("SETCTRLNOTIFY","","70") == 0 ||
	dllfunc("NEWCONTROL","check","check1","チェックボックス") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("NEWCONTROL","radio","radio1","ラジオボックス") == 0 ||
	dllfunc("SETCTRLITEM","","項目１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","項目２","-1") == 0 ||
	dllfunc("SETCTRLSTATE","","1") == 0 ||
	dllfunc("SETCTRLNOTIFY","","20") == 0 ||
	dllfunc("NEWCONTROL","list","list1","リストボックス") == 0 ||
	dllfunc("SETCTRLITEM","","リスト１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト２","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト３","-1") == 0 ||
	dllfunc("SETCTRLNOTIFY","","30,40") == 0 ||
	dllfunc("SETCTRLHEIGHT","",2) == 0 ||
	dllfunc("NEWCONTROL","combo","combo1","コンボボックス") == 0 ||
	dllfunc("SETCTRLITEM","","リスト１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト２","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト３","-1") == 0 ||
	dllfunc("SETCTRLNOTIFY","","50,60") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

SETSTATE_START:
#enable = 1;

#radiostate = 1;
#combostate = 0;

#chgflag = 0;
#chgtitle = 1;

//	ダイアログの表示
if (dllfunc("SHOWDIALOG",hidemaruhandle(0),1) == 0)
	goto ERROR;

$focus_prev = "";
while (1) {
	$name = "";
	while (strlen($name) == 0) { //	ダイアログからの通知を待つお約束な処理
		//	２番目の引数を小さくすると応答性はよくなるが負荷は大きくなる。
		$name = dllfuncstr("WAITCTRLNOTIFY",10);
		$focus = dllfuncstr("GETFOCUSEDCTRL");
		if ($focus_prev != $focus) {
			##r = dllfunc("SETCTRLSTRING", "focus",
						  "フォーカスの場所：" + $focus);
			$focus_prev = $focus;
		}
	}
	##notify = val($name);
	if (##notify <= 1) break; // 「ＯＫ」または「キャンセル」
	else if (##notify == 10) {
//		message "変えないで！！";
		//	このループ処理中に message コマンドを使うとまずいので、
		//	代わりに SHOWMESSAGE 関数を使って下さい。
		##n = dllfunc("SHOWMESSAGE","11","変えないで！！");
		##n = dllfunc("SETCTRLSTATE","check1","0");
	} else if (##notify == 20) {
		if (val(dllfuncstr("GETCTRLSTATE","radio1")) != #radiostate) {
//			question "本当に変えてもいい？";
			//	上と同様の注意です。
			##n = dllfunc("SHOWQUESTION","22","21","本当に変えてもいい？");
		}
	} else if (##notify == 21 || ##notify == 22) {
		//	ラジオボタンの選択値の変更
		if (##notify == 22) {
			#radiostate = 3 - #radiostate; // 1 -> 2, 2 -> 1
			#chgflag = 1;	//	値を変えた場合タイトルを変更
		}
		##n = dllfunc("SETCTRLSTATE","radio1",str(#radiostate));
	} else if (##notify == 40) {
		##n = dllfunc("SHOWMESSAGE","41","変えないで！！");
		##n = dllfunc("SETCTRLSTATE","list1","0");
	} else if (##notify == 50 || ##notify == 60) {
		##n = dllfunc("SHOWQUESTION","61","51","本当に変えてもいい？");
	} else if (##notify == 51) {
		##n = dllfunc("SETCTRLSTATE","combo1",str(#combostate));
	} else if (##notify == 61) {
		#combostate = val(dllfuncstr("GETCTRLSTATE","combo1"));
		#chgflag = 1;
	} else if (##notify == 70) {
		#enable = 1 - #enable;
		##n = dllfunc("ENABLECTRL",#enable,"check1 radio1 list1 combo1");
	}
	if (#chgflag && #chgtitle) {
		//	タイトルを変更
		##n = dllfunc("SETDLGTITLE",
					"フォント/状態/タイトル変更のテスト(更新)"
				);
		#chgtitle = 0;
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
#n = dllfunc("SAVEDIALOG", "c:\\usertemp\\dialog.dat", "ThisSignature");
return;


SETFONT:
//	フォントの装飾の変更

//	タブコントロール内に表示する子ダイアログの作成
if (dllfunc("NEWDLGPAGE","page1",38) == 0 ||
	dllfunc("NEWCONTROL","text","txt1","テキスト") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||	//	"part half"
	dllfunc("NEWCONTROL","check","chk1","チェックボックス") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("NEWCONTROL","rdbtn","rdbtn1","ラジオボタン") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||	//	"part half"
	dllfunc("NEWCONTROL","button","btn1","ただのボタン") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("NEWCONTROL","edit","edt1","エディットボックス") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("NEWCONTROL","list","lst1","") == 0 ||
	dllfunc("SETCTRLHEIGHT","", 1) == 0 ||
	dllfunc("SETCTRLITEM","","リスト１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト２","-1") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("NEWCONTROL","radio","rd1","") == 0 ||
	dllfunc("SETCTRLITEM","","ラジオ項目１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","ラジオ項目２","-1") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0) {
	goto MAKEDLG_ERROR;
}

if (dllfunc("NEWDLGPAGE","page2",38) == 0 ||
	dllfunc("NEWCONTROL","combo","cmb1","コンボボックス") == 0 ||
	dllfunc("SETCTRLITEM","","コンボリスト１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","コンボリスト２","-1") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("NEWCONTROL","lview","lvw1","2:ヘッダ１:ヘッダ２") == 0 ||
	dllfunc("SETCTRLITEM","","リスト,１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト,２","-1") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("NEWCONTROL","chklist","chl1","") == 0 ||
	dllfunc("SETCTRLITEM","","リスト１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト２","-1") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("NEWCONTROL","tree","tr1","") == 0 ||
	dllfunc("SETCTRLITEM","","n1,親項目","root") == 0 ||
	dllfunc("SETCTRLITEM","","n11,子項目","n1") == 0 ||
	dllfunc("SETCTRLHEIGHT","",2) == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0) {
	goto MAKEDLG_ERROR;
}

//	グループボックス内に表示する子ダイアログの作成
if (dllfunc("NEWDLGPAGE","page3",38) == 0 ||
	dllfunc("NEWCONTROL","check","chk1@page3","ボールド体") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("SETCTRLSTATE","","1") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","check","chk2@page3","イタリック体") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("NEWCONTROL","check","chk3@page3","下線") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","check","chk4@page3","打ち消し線") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("NEWCONTROL","text","","色名(または \"#RRGGBB\")：") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","combo","combo1@page3","") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10,10") == 0 ||
	dllfunc("SETCTRLITEM","","black (黒)","-1") == 0 ||
	dllfunc("SETCTRLITEM","","red (赤)","-1") == 0 ||
	dllfunc("SETCTRLITEM","","green (緑)","-1") == 0 ||
	dllfunc("SETCTRLITEM","","blue (青)","-1") == 0 ||
	dllfunc("SETCTRLSTATE", "", "1") == 0 ||
	dllfunc("NEWCONTROL","text","","フォント名：") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","cmblist", "combo2@page3", "") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10,10") == 0 ||
	dllfunc("SETCTRLITEM","","ＭＳ Ｐゴシック","-1") == 0 ||
	dllfunc("SETCTRLITEM","","ＭＳ Ｐ明朝","-1") == 0 ||
	dllfunc("SETCTRLITEM","","ＭＳ ゴシック","-1") == 0 ||
	dllfunc("SETCTRLITEM","","ＭＳ 明朝","-1") == 0 ||
	dllfunc("SETCTRLITEM","","(デフォルト)","-1") == 0 ||
	dllfunc("SETCTRLSTATE", "", "5") == 0) {
	goto MAKEDLG_ERROR;
}

//	親ダイアログの作成
if (dllfunc("NEWDIALOG","フォントの装飾の変更",40,"") == 0 ||
	dllfunc("NEWCONTROL", "tab", "tab1", "") == 0 ||
	dllfunc("SETCTRLITEM","","page1,ページ1","-1") == 0 ||
	dllfunc("SETCTRLITEM","","page2,ページ2","-1") == 0 ||
	dllfunc("SETCTRLSTATE","","1") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("NEWCOLUMN", 40) == 0 ||
	dllfunc("NEWCONTROL","group","grp1","装飾") == 0 ||
	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("SETCTRLITEM","","page3","-1") == 0 ||
	dllfunc("NEWCONTROL", "text", "", "") == 0 || // dummy
	dllfunc("NEWCONTROL", "text", "", "") == 0 || // dummy
	dllfunc("NEWCONTROL", "text", "", "") == 0 || // dummy
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	##notify = val($name);
	if (##notify <= 1) break;
	else if (##notify == 10) {
		//	フォント変更チェックボックス（コンボボックス）が変更された
		$$face = "";
		if (dllfuncstr("GETCTRLSTATE","chk1@page3") == "1")
			$$face = $$face + "b";
		if (dllfuncstr("GETCTRLSTATE","chk2@page3") == "1")
			$$face = $$face + "i";
		if (dllfuncstr("GETCTRLSTATE","chk3@page3") == "1")
			$$face = $$face + "u";
		if (dllfuncstr("GETCTRLSTATE","chk4@page3") == "1")
			$$face = $$face + "s";
		$$color = "";
		##c = val(dllfuncstr("GETCTRLSTATE","combo1@page3"));
		if (##c != 0) {
			//	リストから選択
			$$color = leftstr(dllfuncstr("GETCTRLITEM","combo1@page3",str(##c)),7);
		} else {
			//	エディットボックスに入力
			$$color = dllfuncstr("GETCTRLSTRING","combo1@page3");
		}
		if (midstr($$color, 0, 1) == "#") {
			if (strlen($$color) < 7) continue;
			$$color = leftstr($$color, 7);
		} else {
			##s = strstr($$color, " ");
			if (##s >= 0) $$color = leftstr($$color, ##s);
		}
		$$name = "";
		##c = val(dllfuncstr("GETCTRLSTATE","combo2@page3"));
		if (##c != 0) {
			//	リストから選択
			if (##c == 5) {
				$$name = "";
			} else {
				$$name = dllfuncstr("GETCTRLITEM", "combo2@page3", str(##c));
			}
//		} else {
			//	エディットボックスに入力
//			$$name = dllfuncstr("GETCTRLSTRING","combo2@page3");
		}
		//	フォントの変更
		$$arg = "'" + $$face + "' '" + $$color + "' '" + $$name + "'";
		##n = dllfunc("SETCTRLFONT", "tab1", $$arg);
		##n = dllfunc("SETCTRLFONT", "grp1", $$arg);
		##n = dllfunc("SETCTRLFONT", "txt1", $$arg);
		##n = dllfunc("SETCTRLFONT", "chk1", $$arg);
		##n = dllfunc("SETCTRLFONT", "rdbtn1", $$arg);
		##n = dllfunc("SETCTRLFONT", "btn1", $$arg);
		##n = dllfunc("SETCTRLFONT", "edt1", $$arg);
		##n = dllfunc("SETCTRLFONT", "lst1", $$arg);
		##n = dllfunc("SETCTRLFONT", "rd1", $$arg);
		##n = dllfunc("SETCTRLFONT", "cmb1", $$arg);
		##n = dllfunc("SETCTRLFONT", "lvw1", $$arg);
		##n = dllfunc("SETCTRLFONT", "chl1", $$arg);
		##n = dllfunc("SETCTRLFONT", "tr1", $$arg);
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
return;

MACSRV:
//	マクロサーバ互換コントロールのテスト
if (dllfunc("NEWDIALOG","マクロサーバ互換のコントロール",60,"") == 0 ||
	dllfunc("NEWCONTROL","text","","テキスト") == 0 ||
//	dllfunc("NEWCONTROL","text","focus","フォーカス：") == 0 ||
	dllfunc("NEWCONTROL","check","chk1","チェックボックス") == 0 ||
	dllfunc("NEWCONTROL","edit","edt1","エディットボックス") == 0 ||
	dllfunc("NEWCONTROL","list","lst1","") == 0 ||
	dllfunc("SETCTRLITEM","","リスト項目 １","-1") == 0 ||
//	dllfunc("SETCTRLHEIGHT","",1) == 0 ||
	dllfunc("SETCTRLITEM","","リスト項目 ２","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト項目 ３","-1") == 0 ||
	dllfunc("SETCTRLSTATE","","1") == 0 ||
//	dllfunc("SETFOCUSEDCTRL", "lst1") == 0 ||
	dllfunc("NEWCONTROL","button","btn1","項目追加") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","button","btn2","項目削除") == 0 ||
	dllfunc("SETCTRLNOTIFY","","11") == 0 ||
	dllfunc("NEWCONTROL","radio","rd1","ラジオボタン") == 0 ||
	dllfunc("SETCTRLITEM","","ラジオ項目１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","ラジオ項目２","-1") == 0 ||
	dllfunc("SETCTRLITEM","","ラジオ項目３","-1") == 0 ||
	dllfunc("SETCTRLSTATE","","1") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

MACSRV_START:
if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
##n = 3;
while (1) {
	$name = "";
	while (strlen($name) == 0) {
		$name = dllfuncstr("WAITCTRLNOTIFY",10);
//		$focus = dllfuncstr("GETFOCUSEDCTRL");
//		if ($focus_prev != $focus) {
//			##r = dllfunc("SETCTRLSTRING", "focus",
//						  "フォーカス：" + $focus);
//			$focus_prev = $focus;
//		}
	}
	if ($name == "0" || $name == "1") break;
	else if ($name == "10") {
		//	項目の追加
		##r = dllfunc("SETCTRLITEM","lst1","リスト項目"+char(ascii("１")+##n),"0");
		##n = ##n + 1;
	} else if ($name == "11") {
		//	項目のクリア
		##r = dllfunc("RESETCTRLITEM","lst1");
		##n = 0;
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
message	"チェックボックス：\t" + dllfuncstr("GETCTRLSTATE","chk1") + "\n" + 
		"エディットボックス：\t" + dllfuncstr("GETCTRLSTRING","edt1") + "\n" + 
		"リストボックス：\t" + dllfuncstr("GETCTRLSTRING","lst1") + "\n" + 
		"ラジオボタン：\t" + dllfuncstr("GETCTRLSTATE","rd1");

return;


REFBUTTON:
//	参照ボタンのテスト
if (dllfunc("NEWDIALOG","「参照」ボタンのテスト",60,"") == 0 ||
	dllfunc("NEWCONTROL","text","","ファイルの参照") == 0 ||
	dllfunc("SETCTRLWIDTH","",16) == 0 ||
	dllfunc("NEWCONTROL","edit","file","") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","reffilebutton","fileref","") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("SETCTRLITEM","","") == 0 ||	//	ダミー
	dllfunc("SETCTRLITEM","",hidemarudir) == 0 ||
	dllfunc("SETCTRLITEM","","'実行ファイル (*.exe;*.com)'") == 0 ||
	dllfunc("SETCTRLITEM","","'テキストファイル (*.txt)'") == 0 ||
	dllfunc("SETCTRLITEM","","1") == 0 ||
	dllfunc("NEWCONTROL","text","","フォルダの参照") == 0 ||
	dllfunc("SETCTRLWIDTH","",16) == 0 ||
	dllfunc("NEWCONTROL","edit","dir","") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","refdirbutton","dirref","") == 0 ||
	dllfunc("SETCTRLNOTIFY","","11") == 0 ||
	dllfunc("SETCTRLITEM","","フォルダを選んで下さい") == 0 ||
	dllfunc("SETCTRLITEM","",hidemarudir) == 0 ||
	dllfunc("NEWCONTROL","text","","色の参照") == 0 ||
	dllfunc("SETCTRLWIDTH","",16) == 0 ||
	dllfunc("NEWCONTROL","edit","color","") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","refcolorbutton","colorref","") == 0 ||
	dllfunc("SETCTRLNOTIFY","","12") == 0 ||
	dllfunc("SETCTRLITEM","","色を選んで下さい") == 0 ||
	dllfunc("SETCTRLITEM","","black") == 0 ||
	dllfunc("NEWCONTROL","text","","フォントの参照") == 0 ||
	dllfunc("SETCTRLWIDTH","",16) == 0 ||
	dllfunc("NEWCONTROL","edit","font","") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","reffontbutton","fontref","") == 0 ||
	dllfunc("SETCTRLNOTIFY","","13") == 0 ||
	dllfunc("SETCTRLITEM","","フォントを選んで下さい") == 0 ||
	dllfunc("SETCTRLITEM","","'Times New Roman'") == 0 ||
	dllfunc("SETCTRLITEM","","12") == 0 ||
	dllfunc("SETCTRLITEM","","red") == 0 ||
	dllfunc("SETCTRLITEM","","biu") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

REFBUTTON_START:
if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
$$user_colors = "";
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	##notify = val($name);
	if (##notify <= 1) break;
	else if (##notify == 10) {
		//	ファイルの参照
		$$ret = dllfuncstr("GETCTRLSTRING","fileref");
		##r = dllfunc("SETCTRLSTRING","file",$$ret);
		if (strstr($$ret,"|") != -1)
			$$ret = leftstr($$ret,strstr($$ret,"|"));
		##r = dllfunc("CHANGECTRLITEM","fileref",$$ret,"2");
	} else if (##notify == 11) {
		//	フォルダの参照
		$$ret = dllfuncstr("GETCTRLSTRING","dirref");
		##r = dllfunc("SETCTRLSTRING","dir",$$ret);
		##r = dllfunc("CHANGECTRLITEM","dirref",$$ret,"2");
	} else if (##notify == 12) {
		//	色の参照
		$$ret = dllfuncstr("GETCTRLSTRING","colorref");
		if (strlen($$ret) > 7) {
			$$user_colors = midstr($$ret,7,strlen($$ret)-7);
			$$ret = leftstr($$ret,7);
		}
		##r = dllfunc("SETCTRLSTRING","color",$$ret);
		##r = dllfunc("CHANGECTRLITEM","colorref",$$ret+$$user_colors,"2");
	} else if (##notify == 13) {
		//	フォントの参照
		$$ret = dllfuncstr("GETCTRLSTRING","fontref");
		##r = dllfunc("SETCTRLSTRING","font",$$ret);
		$$fontname = "'" + dllfuncstr("GETTOKEN",$$ret,",") + "'";
		$$fontsize = dllfuncstr("GETTOKEN","",",");
		$$fontcolor = dllfuncstr("GETTOKEN","",",");
		$$fonttype = dllfuncstr("GETTOKEN","","");
		##r = dllfunc("CHANGECTRLITEM","fontref",$$fontname,"2");
		##r = dllfunc("CHANGECTRLITEM","fontref",$$fontsize,"3");
		##r = dllfunc("CHANGECTRLITEM","fontref",$$fontcolor,"4");
		##r = dllfunc("CHANGECTRLITEM","fontref",$$fonttype,"5");
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
message "ファイル名：\t" + dllfuncstr("GETCTRLSTRING","file") + "\n" + 
		"フォルダ名：\t" + dllfuncstr("GETCTRLSTRING","dir") + "\n" + 
		"色のRGB値：\t" + dllfuncstr("GETCTRLSTRING","color") + "\n" + 
		"フォント：\t" + dllfuncstr("GETCTRLSTRING","font");
return;


COMBOBOX:
//	コンボボックスのテスト
if (dllfunc("NEWDIALOG","コンボボックス・ドロップダウンリストのテスト",40) == 0 ||
	dllfunc("NEWCONTROL","text","","コンボボックス：") == 0 ||
	dllfunc("SETCTRLWIDTH","",15) == 0 ||
	dllfunc("NEWCONTROL","combo","combo1","初期テキスト") == 0 ||
	dllfunc("SETCTRLITEM","","リスト１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト２","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト３","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト４","-1") == 0 ||
	dllfunc("NEWCONTROL","button","btn1","追加") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","button","btn2","変更") == 0 ||
	dllfunc("SETCTRLNOTIFY","","11") == 0 ||
	dllfunc("NEWCONTROL","button","btn3","削除") == 0 ||
	dllfunc("SETCTRLNOTIFY","","12") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","button","btn4","全削除") == 0 ||
	dllfunc("SETCTRLNOTIFY","","13") == 0 ||
	dllfunc("NEWCONTROL","text","","ドロップダウン：") == 0 ||
	dllfunc("SETCTRLWIDTH","",15) == 0 ||
	dllfunc("NEWCONTROL","cmblist","cmbl1","初期テキスト") == 0 ||
	dllfunc("SETCTRLITEM","","リスト１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト２","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト３","-1") == 0 ||
	dllfunc("SETCTRLITEM","","リスト４","-1") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

COMBOBOX_START:
if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1))
	goto ERROR;
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	if ($name == "0" || $name == "1") break;
	else if ($name == "10") {
		//	項目の追加
		$item = input("項目テキスト,追加する場所(1以上)\n場所に 0 を指定すると\n一番後ろに追加します","テキスト,1");
		if ((!result) || $item == "") continue;
		##s = strstr($item,",");
		if (##s == -1) ##s = strlen($item);
		##r = dllfunc("SETCTRLITEM",
					"combo1",
					leftstr($item,##s),
					midstr($item,##s+1,255)
				);
	} else if ($name == "11") {
		//	項目の変更
		$item = input("項目テキスト,変更する場所(1以上)\n場所に 0 を指定すると現在の選択項目、\n-1 を指定すると一番最後の項目を変更します","テキスト,1");
		if ((!result) || $item == "") continue;
		##s = strstr($item,",");
		if (##s == -1) ##s = strlen($item);
		##r = dllfunc("CHANGECTRLITEM",
					"combo1",
					leftstr($item,##s),
					midstr($item,##s+1,255)
				);
	} else if ($name == "12") {
		//	項目の削除
		$item = input("削除する場所(1以上)\n場所に 0 を指定すると現在の選択項目、\n-1 を指定すると一番最後の項目を削除します","1");
		if ((!result) || $item == "") continue;
		##r = dllfunc("DELETECTRLITEM","combo1",$item);
	} else if ($name == "13") {
		//	項目の全削除
		##r = dllfunc("RESETCTRLITEM","combo1");
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
message "getstate  の結果：\t" + dllfuncstr("GETCTRLSTATE","combo1") + "\n" + 
		"getstring の結果：\t" + dllfuncstr("GETCTRLSTRING","combo1") + "\n" + 
		"getstate  の結果：\t" + dllfuncstr("GETCTRLSTATE","cmbl1") + "\n" + 
		"getstring の結果：\t" + dllfuncstr("GETCTRLSTRING","cmbl1");
return;


SPINBUTTON:
//	スピンボタンのテスト
if (dllfunc("NEWDIALOG","スピンボタンのテスト",30) == 0 ||
	dllfunc("NEWCONTROL","text","","-5〜5の値") == 0 ||
	dllfunc("SETCTRLWIDTH","",10) == 0 ||
	dllfunc("NEWCONTROL","spin","spin1","-5:5") == 0 ||
	dllfunc("SETCTRLSTATE","","0") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

SPINBUTTON_START:
if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
$name = "";
while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
if (!dllfunc("ENDDIALOG")) goto ERROR;
message "getstate  の結果：\t" + dllfuncstr("GETCTRLSTATE","spin1") + "\n" + 
		"getstring の結果：\t" + dllfuncstr("GETCTRLSTRING","spin1");
return;


TRACKBAR:
//	トラックバーのテスト
if (dllfunc("NEWDIALOG","トラックバーのテスト",50) == 0 ||
	dllfunc("NEWCONTROL","text","","０−４、目盛幅１") == 0 ||
	dllfunc("NEWCONTROL","track","track1","0:4:1") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10,11") == 0 ||
	dllfunc("NEWCONTROL","text","","０−８、目盛幅２") == 0 ||
	dllfunc("NEWCONTROL","track","track2","0:8:2") == 0 ||
	dllfunc("SETCTRLNOTIFY","","20,21") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

TRACKBAR_START:
if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	if ($name == "1" || $name == "0") {
		break;
	} else if ($name == "10" || $name == "20") {
		##r = dllfunc("SHOWMESSAGE","30","クリックまたはキーによる移動");
	} else if ($name == "11" || $name == "21") {
		##r = dllfunc("SHOWMESSAGE","30","ドラッグによる移動");
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
message "getstate の結果：\t" + dllfuncstr("GETCTRLSTATE","track1") + "\n" + 
		"getstate の結果：\t" + dllfuncstr("GETCTRLSTATE","track2");
return;

LISTVIEW:
if (dllfunc("NEWDIALOG","リストビューのテスト",50) == 0 ||
	dllfunc("NEWCONTROL","text","","ヘッダあり") == 0 ||
	dllfunc("NEWCONTROL","lview","lview1","3:項目１:項目２:項目３") == 0 ||
	dllfunc("SETCTRLITEM","","1-1,1-2,1-3","-1") == 0 ||
	dllfunc("SETCTRLITEM","","2-1,2-2,2-3","-1") == 0 ||
	dllfunc("SETCTRLITEM","","3-1,3-2,3-3","-1") == 0 ||
	dllfunc("SETCTRLSTATE","","1,3") == 0 ||
	dllfunc("SETCTRLNOTIFY", "", "65535,65535,30,31,32") == 0 ||
	dllfunc("NEWCONTROL","button","","変更") == 0 ||
	dllfunc("SETCTRLNOTIFY","","20") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","button","","削除") == 0 ||
	dllfunc("SETCTRLNOTIFY","","21") == 0 ||
	dllfunc("NEWCONTROL","text","","ヘッダなし") == 0 ||
	dllfunc("NEWCONTROL","lview","lview2","4") == 0 ||
	dllfunc("SETCTRLITEM","","■,□,■,□","-1") == 0 ||
	dllfunc("SETCTRLITEM","","□,■,□,■","-1") == 0 ||
	dllfunc("SETCTRLITEM","","■,□,■,□","-1") == 0 ||
	dllfunc("NEWCONTROL","button","","追加") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

LISTVIEW_START:
##i = 1;

if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	if ($name == "1" || $name == "0") break;
	else if ($name == "10") {
		//	項目の追加
		$addstr = midstr("■ □ ■ □ ■",##i*3,11);
		##r = dllfunc("SETCTRLITEM","lview2",$addstr,"-1");
		##i = 1 - ##i;
	} else if ($name == "20") {
		//	項目の変更
		$$s = leftstr(dllfuncstr("GETCTRLSTATE","lview1"),1);
		if ($$s == "") {
			##r = dllfunc("SHOWMESSAGE",
						"99",
						"どれかを選択した後に押して下さい。"
					);
			continue;
		}
		$item = dllfuncstr("GETCTRLITEM","lview1",$$s);
		$item = input("項目ﾃｷｽﾄ1,項目ﾃｷｽﾄ2,項目ﾃｷｽﾄ3",$item);
		if ((!result) || $item == "") continue;
		##r = dllfunc("CHANGECTRLITEM","lview1",$item,$$s);
	} else if ($name == "21") {
		//	項目の削除
		##r = dllfunc("DELETECTRLITEM","lview1","0");
	} else if ($name == "30" || $name == "31" || $name == "32") {
		##v = val($name) - 29;
		##r = dllfunc("SHOWMESSAGE", "99", "項目" + str(##v) + "をクリック");
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
message "getstate の結果：\t" + dllfuncstr("GETCTRLSTATE","lview1") + "\n" + 
		"getstate の結果：\t" + dllfuncstr("GETCTRLSTATE","lview2");
return;

CHECKLIST:
if (dllfunc("NEWDIALOG","チェックリストのテスト",50) == 0 ||
	dllfunc("NEWCONTROL","chklist","chklst1","") == 0 ||
	dllfunc("SETCTRLITEM","","チェック項目１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","チェック項目２","-1") == 0 ||
	dllfunc("SETCTRLITEM","","チェック項目３","-1") == 0 ||
	dllfunc("SETCTRLSTATE","","1,3") == 0 ||
	dllfunc("NEWCONTROL","button","btn1","追加") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("SETCTRLWIDTH","",-1) == 0 ||
	dllfunc("NEWCONTROL","button","btn2","削除") == 0 ||
	dllfunc("SETCTRLNOTIFY","","20") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

CHECKLIST_START:
##n = 0;

if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	if ($name == "0" || $name == "1") break;
	else if ($name == "10") {
		//	項目の追加
		##r = dllfunc("SETCTRLITEM",
					"chklst1",
					"追加項目" + char(ascii("１")+##n),
					"0"
				);
		##n = ##n + 1;
	} else if ($name == "20") {
		//	項目の削除
		$item = dllfuncstr("GETCTRLSTATE","chklst1");
		if ($item != "") {
			##s = strstr($item,",");
			if (##s != -1) $item = leftstr($item,##s);
			##r = dllfunc("DELETECTRLITEM","chklst1",$item);
		}
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
message "getstate の結果：\t" + dllfuncstr("GETCTRLSTATE","chklst1") + "\n" + 
		"getctrlfocuseditem の結果：\t" + dllfuncstr("GETCTRLFOCUSEDITEM", "chklst1");
return;


MLEDIT:
if (dllfunc("NEWDIALOG","拡張エディットボックスのテスト",50) == 0 ||
	dllfunc("NEWCONTROL","text","","パスワード入力：") == 0 ||
	dllfunc("SETCTRLWIDTH","",16) == 0 ||
	dllfunc("NEWCONTROL","pwdedit","pwdedt1","password") == 0 ||
	dllfunc("NEWCONTROL","mledit","mledt1",
			"複数行\nエディット\nコントロール") == 0 ||
	dllfunc("SETCTRLHEIGHT","",5) == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

MLEDIT_START:
if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
$name = "";
while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
if (!dllfunc("ENDDIALOG")) goto ERROR;
message "pwdedit::getstring の結果：\n" + 
		dllfuncstr("GETCTRLSTRING","pwdedt1") + "\n" + 
		"mledit::getstring の結果：\n" + 
		dllfuncstr("GETCTRLSTRING","mledt1");
return;

FRAME:
//	フレームコントロールのテスト
if (dllfunc("NEWDLGPAGE","page1",50) == 0 ||
	dllfunc("NEWCONTROL","text","","ページ１") == 0 ||
	dllfunc("NEWCONTROL","button","","ページ１のボタン") == 0 ||
	dllfunc("NEWCONTROL","edit","","ページ１のエディットボックス") == 0 ||
	dllfunc("NEWCONTROL","check","","ページ１のチェックボックス") == 0) {
	goto MAKEDLG_ERROR;
}

if (dllfunc("NEWDLGPAGE","page2",50) == 0 ||
	dllfunc("NEWCONTROL","text","","ページ２") == 0 ||
	dllfunc("NEWCONTROL","radio","rd1@page2","ページ２のラジオボタン") == 0 ||
	dllfunc("SETCTRLITEM","","ラジオ項目１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","ラジオ項目２","-1") == 0) {
	goto MAKEDLG_ERROR;
}

if (dllfunc("NEWDIALOG","フレームコントロールのテスト",50,"") == 0 ||
	dllfunc("NEWCONTROL","text","",
			"ウィザードの様なダイアログが作れます") == 0 ||
	dllfunc("NEWCONTROL","frame","frame1","") == 0 ||
	dllfunc("SETCTRLITEM","","page1","-1") == 0 ||
	dllfunc("SETCTRLITEM","","page2","-1") == 0 ||
	dllfunc("SETCTRLSTATE","","1") == 0 ||
	dllfunc("NEWCONTROL","button","","<< 戻る") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("SETCTRLWIDTH","",10) == 0 ||
	dllfunc("NEWCONTROL","button","","進む >>") == 0 ||
	dllfunc("SETCTRLNOTIFY","","11") == 0 ||
	dllfunc("SETCTRLWIDTH","",10) == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

FRAME_START:
if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	if ($name == "1" || $name == "0") break;
	else if ($name == "10") {
		##r = dllfunc("SETCTRLSTATE","frame1","1");
	} else if( $name == "11" ){
		##r = dllfunc("SETCTRLSTATE","frame1","2");
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
return;


GROUP:
//	グループボックスのテスト
if (dllfunc("NEWDLGPAGE","page1",48) == 0 ||
	dllfunc("NEWCONTROL","rdbtn","rd1@page1","ボタン１") == 0 ||
	dllfunc("SETCTRLNOTIFY","","11") == 0 ||
	dllfunc("SETCTRLSTATE","","1") == 0 ||
	dllfunc("NEWCONTROL","rdbtn","rd2@page1","ボタン２") == 0 ||
	dllfunc("SETCTRLNOTIFY","","12") == 0 ||
	dllfunc("SETCTRLSTATE","","0") == 0 ||
	dllfunc("NEWCONTROL","rdbtn","rd3@page1","ボタン３") == 0 ||
	dllfunc("SETCTRLNOTIFY","","13") == 0 ||
	dllfunc("SETCTRLSTATE","","0") == 0 ||
	dllfunc("SETCTRLWIDTH","",10) == 0 ||
	dllfunc("NEWCONTROL","edit","ed1@page1","エディット") == 0) {
	goto MAKEDLG_ERROR;
}

if (dllfunc("NEWDIALOG","グループボックスのテスト",50,"") == 0 ||
	dllfunc("NEWCONTROL","group","grp1","グループボックス") == 0 ||
	dllfunc("SETCTRLITEM","","page1","-1") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

GROUP_START:
$c = "1";
##r = dllfunc("ENABLECTRL",0,"ed1@page1");

if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	if (strlen($name) == 1) break;
	else if (leftstr($name,1) == "1") {
		##r = dllfunc("SETCTRLSTATE","rd"+$c+"@page1","0");
		$c = rightstr($name,1);
		##r = dllfunc("SETCTRLSTATE","rd"+$c+"@page1","1");
		if ($c == "3") ##r = dllfunc("ENABLECTRL",1,"ed1@page1");
		else ##r = dllfunc("ENABLECTRL",0,"ed1@page1");
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
message "getstate の結果１：\t"+dllfuncstr("GETCTRLSTATE","rd1@page1")+"\n" + 
		"getstate の結果２：\t"+dllfuncstr("GETCTRLSTATE","rd2@page1")+"\n" + 
		"getstate の結果３：\t"+dllfuncstr("GETCTRLSTATE","rd3@page1")+"\n" + 
		"getstringの結果：\t" + dllfuncstr("GETCTRLSTRING","ed1@page1");
return;


TAB:
//	タブコントロールのテスト
if (dllfunc("NEWDLGPAGE","page1",48) == 0 ||
	dllfunc("NEWCONTROL","text","","ページ１") == 0 ||
	dllfunc("NEWCONTROL","button","","ページ１のボタン") == 0 ||
	dllfunc("NEWCONTROL","edit","","ページ１のエディットボックス") == 0 ||
	dllfunc("NEWCONTROL","check","","ページ１のチェックボックス") == 0) {
	goto MAKEDLG_ERROR;
}

if (dllfunc("NEWDLGPAGE","page2",48) == 0 ||
	dllfunc("NEWCONTROL","text","","ページ２") == 0 ||
	dllfunc("NEWCONTROL","radio","rd1@page2","ページ２のラジオボタン") == 0 ||
	dllfunc("SETCTRLITEM","","ラジオ項目１","-1") == 0 ||
	dllfunc("SETCTRLITEM","","ラジオ項目２","-1") == 0) {
	goto MAKEDLG_ERROR;
}

if (dllfunc("NEWDIALOG","タブコントロールのテスト",50,"") == 0 ||
	dllfunc("NEWCONTROL","text","",
			"プロパティシートの様なダイアログも作れます") == 0 ||
	dllfunc("NEWCONTROL","tab","tab1","") == 0 ||
	dllfunc("SETCTRLITEM","","page1,ページ1(選択)","-1") == 0 ||
	dllfunc("SETCTRLITEM","","page2,ページ2　　　","-1") == 0 ||
	dllfunc("SETCTRLSTATE","","1") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
//	dllfunc("SETCTRLFONT","","b") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

TAB_START:

if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	if ($name == "0" || $name == "1") break;
	else if ($name == "10") {
		$$s = dllfuncstr("GETCTRLSTATE","tab1");
		##r = dllfunc("CHANGECTRLITEM","tab1","ページ"+$$s+"(選択)",$$s);
		$$s = str(3-val($$s));
		##r = dllfunc("CHANGECTRLITEM","tab1","ページ"+$$s+"　　　",$$s);
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
return;


TREE:
//	ツリービューコントロールのテスト
if (dllfunc("NEWDIALOG","ツリービューのテスト",60,"") == 0 ||
	dllfunc("NEWCONTROL","tree","tree1","") == 0 ||
	dllfunc("SETCTRLITEM","","n1,親項目１","root") == 0 ||
	dllfunc("SETCTRLITEM","","n2,親項目２","root") == 0 ||
	dllfunc("SETCTRLITEM","","n11,親１の子項目１","n1") == 0 ||
	dllfunc("SETCTRLITEM","","n12,親１の子項目２","n1") == 0 ||
	dllfunc("SETCTRLITEM","","n111,親１の子項目１の孫項目１","n11") == 0 ||
	dllfunc("SETCTRLITEM","","n21,親２の子項目１","n2") == 0 ||
	dllfunc("SETCTRLITEM","","n22,親２の子項目２","n2") == 0 ||
	dllfunc("SETCTRLITEM","","n23,親２の子項目３","n2") == 0 ||
	dllfunc("SETCTRLSTATE","","n111") == 0 ||
	dllfunc("SETCTRLHEIGHT","",7) == 0 ||
	dllfunc("NEWCONTROL","button","btn1","追加") == 0 ||
	dllfunc("SETCTRLNOTIFY","","10") == 0 ||
	dllfunc("SETCTRLWIDTH","",20) == 0 ||
	dllfunc("NEWCONTROL","button","btn2","変更") == 0 ||
	dllfunc("SETCTRLNOTIFY","","11") == 0 ||
	dllfunc("SETCTRLWIDTH","",20) == 0 ||
	dllfunc("NEWCONTROL","button","btn3","削除") == 0 ||
	dllfunc("SETCTRLNOTIFY","","12") == 0 ||
	dllfunc("NEWCONTROL","okcancel","","") == 0) {
	goto MAKEDLG_ERROR;
}

TREE_START:
##i = 1;

if (!dllfunc("SHOWDIALOG",hidemaruhandle(0),1)) goto ERROR;
while (1) {
	$name = "";
	while (strlen($name) == 0) $name = dllfuncstr("WAITCTRLNOTIFY",10);
	if ($name == "1" || $name == "0") break;
	else if ($name == "10") {
		$par = dllfuncstr("GETCTRLSTATE","tree1");
		if ($par == "") continue;
		$this = "a" + str(##i);
		##r = dllfunc("SETCTRLITEM",
					"tree1",
					$this + ",追加項目" + str(##i),
					$par
				);
		if (##r != 0) ##i = ##i + 1;
	} else if ($name == "11") {
		$item = dllfuncstr("GETCTRLITEM","tree1","");
		$item = input("項目テキスト",$item);
		if (!result) continue;
		##r = dllfunc("CHANGECTRLITEM",
					"tree1",
					$item,
					dllfuncstr("GETCTRLSTATE","tree1")
				);
	} else if ($name == "12") {
		$item = dllfuncstr("GETCTRLSTATE","tree1");
		if ($item != "") ##r = dllfunc("DELETECTRLITEM","tree1",$item);
	}
}
if (!dllfunc("ENDDIALOG")) goto ERROR;
$item = dllfuncstr("GETCTRLSTATE","tree1");
message "getstate の結果：\t" + $item + "\n" + 
		"getitem  の結果：\t" + dllfuncstr("GETCTRLITEM","tree1",$item);

return;

